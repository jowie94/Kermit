grammar Kermit;

options {
	language=CSharp3;
	TokenLabelType=CommonToken;
	output=AST;
	ASTLabelType=KermitAST;
}

tokens {
	BLOCK; ARGS; CALL; DOT='.'; REF='ref'; INDEX='['; ARR; RPRINT;
	FUNC='function'; RETURN='return'; IF='if'; WHILE='while'; FOR='for'; NEW='new'; // Keywords
	EQ='=='; LT='<'; BT='>'; LTE='<='; BTE='>='; NE='!='; TRUE='true'; FALSE='false'; // Logical (TODO: Add more logical tokens)
	NUM; ASSIGN='='; ADD='+'; SUB='-'; MUL='*'; DIV='/'; // Arithmetics
}

@lexer::namespace{Kermit.Parser}
@parser::namespace{Kermit.Parser}

@members {
	IScope currentScope;
	public KermitParser(ITokenStream input, IScope globalScope) : this(input)
	{
		currentScope = globalScope;
	}
}

/*
 * Parser Rules
 */

public
program
	: (functionDefinition|statementOrNL)+ EOF
	  -> ^(BLOCK statementOrNL*)
	;

functionDefinition
	: 'function' ID
	 {
		FunctionSymbol fs = new FunctionSymbol($ID.text, currentScope);
		currentScope.Define(fs);
		currentScope = fs;
	 }
	 '(' (vardef (',' vardef)* )? ')'
	 {currentScope = new LocalScope(fs);}
	 multipleStatements
	 {
		fs.BlockAst = $multipleStatements.tree;
		currentScope = currentScope.EnclosingScope;
		currentScope = currentScope.EnclosingScope;
	 }
	 ->
	;

vardef
	: ID
	 {
		$ID.tree.Scope = currentScope;
		VariableSymbol vs = new VariableSymbol($ID.text);
		try
		{
			currentScope.Define(vs);
		} catch (System.ArgumentException e)
		{
			IToken tok = input.LT(-1);
			ThrowHelper.AlreadyDefinedArg(SourceName, tok.Line, CreateInputError(tok.Line, tok.CharPositionInLine), $ID.text, e);
		}
	 }
	;

soms // Single or multiple statements
	: NL* statement	-> ^(BLOCK statement)
	| multipleStatements
	;

multipleStatements
	: NL* '{' statementOrNL* '}' NL*	-> ^(BLOCK statementOrNL*)
	;

statementOrNL // Extended statements (include newline)
	: statement
	| NL		->
	;

statement // Normal statements
	: 'return' expr NL		-> ^('return' expr)
	| 'if' '(' expr ')' c=soms ('else' el=soms)?			-> ^('if' expr $c $el?)
	| 'while' '(' expr ')' soms								-> ^('while' expr soms)
	| 'do' ms=multipleStatements 'while' '(' expr ')'		-> ^(BLOCK $ms ^(WHILE expr $ms))
	| 'for' '(' beg=equalOrId ';' cond=expr ';' wh=equalOrId ')' soms	-> ^('for' $beg $cond $wh soms)
	| expr NL				-> ^(RPRINT expr)
	;

refOrExpr
	: 'ref' qid	-> ^('ref' qid)
	| expr
	;

expr: addexpr (('=='|'!='|'<'|'>'|'<='|'>=')^ addexpr)? ;

addexpr
	: mulexpr (('+'|'-')^ mulexpr)*
	;

mulexpr
	: notOrAtom (('*'|'/')^ notOrAtom)*
	;

notOrAtom
	: NOT notOrAtom -> ^(NOT notOrAtom)
	| qatom
	;

qatom
	: equalOrId | atom ('.'^ callOrIdArray)* ;

atom
	: '-'? n=(INT|FLOAT) -> ^(NUM $n '-'?)
	| CHAR
	| ('true'|'false')
	| primary ('['^ expr ']'!)?
	//| call ('['^ el=expr ']'!)?
	//| callOrIdArray
	;

equalOrId
	: qid 
		(
			-> qid
			| {input.LT(-1).Text != ")"}? ('=' expr) -> ^('=' qid expr)
			| {input.LT(-1).Text != ")"}? ex=('+'|'-'|'*'|'/')  ('=' expr) -> ^('=' qid ^($ex qid expr))
		)
	;

qid
	: callOrIdArray ('.'^ callOrIdArray)*
	; // Can't be resolved until runtime

primary
	: STRING
	| instance
	| call
	| ID
	| '[' (expr (',' expr )*)? ']' -> ^(ARR expr*)
	| '(' expr ')' -> expr
	;

instance
@after {
	KermitAST nameNode = (KermitAST) $instance.tree.GetChild(0);
	nameNode.Scope = currentScope;
}
	:	'new' ID '(' (expr (',' expr )*)? ')' // TODO: Future improvement, support subclasses
		-> ^('new' ID expr*)
	;

callOrIdArray
	: call ('['^ el=expr ']'!)?
	| idArray
	;

idArray	: ID ('['^ el=expr ']'!)? ;

	//:callOrId ('['^ el=expr ']'!)?;

callOrId : call | ID ;

call
@after {
	$call.tree.Scope = currentScope;
}
	: name=ID '(' (refOrExpr (',' refOrExpr)*)? ')' -> ^(CALL ID refOrExpr*) ;

/*
 * Lexer Rules
 */

WS		:   (' '|'\t') {$channel=Hidden;};

NL		:	'\r'? '\n' ;

ID		:   LETTER (LETTER | '0'..'9' | '-' | '_')*  ;

fragment
LETTER
	:   ('a'..'z' | 'A'..'Z')
	;

CHAR	:	'\'' . '\'' ;

STRING	:	'\"' .* '\"' ;

AND		:	('and' | '&&') ;

OR		:	('or' | '||') ;

NOT		:	('not' | '!') ;

INT		:	'0'..'9'+ ;

FLOAT
	:	INT '.' INT*
	|	'.' INT+
	;

SL_COMMENT
	:   '//' ~('\r'|'\n')* {$channel=Hidden;}
	|	'/*' .* '*/' {$channel=Hidden;}
	;
